// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        ext.kotlin_version = '1.6.0'

        support_lib_version = '23.4.0'
        okhttp_version = '2.5.0'

        android_build_sdk_version = 31
        android_build_tools_version = '31.0.3'
        android_build_target_sdk_version = 31
        android_build_min_sdk_version = 26
    }

    repositories {
        jcenter()
        mavenCentral()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.28.0'
        classpath 'com.google.gms:google-services:4.1.0'
        //Dexcount gradle plugin
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.4.4'
        classpath "com.google.android.libraries.mapsplatform.secrets-gradle-plugin:secrets-gradle-plugin:2.0.1"
    }
}

def getMavenUsername(){
    return hasProperty('COM_O3DR_MAVEN_USERNAME') ? COM_O3DR_MAVEN_USERNAME : ''
}

def getMavenApiKey(){
    return hasProperty('COM_O3DR_MAVEN_APIKEY') ? COM_O3DR_MAVEN_APIKEY : ''
}

def getMavenRepoUrl(){
    return hasProperty('COM_O3DR_MAVEN_REPO_URL') ? COM_O3DR_MAVEN_REPO_URL :
            'https://dl.bintray.com/3d-robotics/maven'
}

def computeVersionCode(int versionMajor, int versionMinor, int versionPatch, int versionBuild = 0){
    return versionMajor * 100000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
}

def generateVersionName(String versionPrefix, int versionMajor, int versionMinor, int versionPatch, String versionSuffix = ""){
    def versionName = "${versionPrefix}${versionMajor}.${versionMinor}.${versionPatch}"

    if(versionSuffix != null && !versionSuffix.isEmpty() && versionSuffix != "release"){
        versionName += "-${versionSuffix}"
    }

    return versionName
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven {
            url getMavenRepoUrl()
        }
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }

        flatDir {
            dirs 'libs'
        }
    }
}